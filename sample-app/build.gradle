buildscript {
  repositories {
    google()
    jcenter()
  }
  dependencies {
    classpath deps.realmPlugin
  }
}

apply plugin: "com.android.application"
apply plugin: "kotlin-android"
apply plugin: "kotlin-kapt"
apply plugin: "kotlin-android-extensions"
apply plugin: "realm-android"

android {
  compileSdkVersion buildDetails.compileSdk

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }

  kotlinOptions {
    jvmTarget = "1.8"
  }

  defaultConfig {
    applicationId "dev.arunkumar.android"
    minSdkVersion 21
    targetSdkVersion buildDetails.targetSdk

    versionCode 1
    versionName "1.0"

    vectorDrawables.useSupportLibrary = true

    multiDexEnabled true

    testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
  }

  buildTypes {
    debug {
      minifyEnabled false
      shrinkResources false
      debuggable true
      proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"
    }
    release {
      minifyEnabled true
      shrinkResources true
      debuggable false
      proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"
    }
  }

  buildTypes.all { buildType ->
    buildType
        .javaCompileOptions
        .annotationProcessorOptions
        .arguments = [
        logEpoxyTimings              : "true",
        enableParallelEpoxyProcessing: "true"
    ]
  }

  flavorDimensions "dep"

  productFlavors {
    local {
      dimension "dep"
    }
    remote {
      dimension "dep"
    }
  }

  variantFilter { variant ->
    if (variant.name == "localRelease") {
      variant.setIgnore(true)
    }
  }

  ndkVersion "21.3.6528147"

  testOptions {
    unitTests {
      includeAndroidResources = true
    }
  }
}

androidExtensions {
  experimental = true
}

dependencies {
  implementation fileTree(dir: "libs", include: ["*.jar"])

  implementation deps.workManagerRx
  implementation deps.workManagerKtx

  implementation "androidx.multidex:multidex:2.0.1"

  implementation deps.daggerAndroid
  implementation deps.daggerAndroidSupport
  kapt deps.daggerCompiler
  kapt deps.daggerAndroidProcessor
  kaptTest deps.daggerCompiler
  kaptTest deps.daggerAndroidProcessor

  // Kotlin
  implementation deps.kotlinStdLib

  // Rx
  implementation deps.rxAndroid
  implementation deps.rxKotlin
  implementation deps.rxBinding

  // Epoxy
  implementation deps.epoxyPaging
  kapt deps.epoxyProcessor

  // AndroidX
  implementation deps.androidXAppCompat
  implementation deps.constraintLayout

  // Lifecycle extensions
  implementation deps.androidXLifecycle

  implementation deps.orbit
  implementation deps.orbitAndroid

  def autodispose = "1.4.0"
  implementation "com.uber.autodispose:autodispose:$autodispose"
  implementation "com.uber.autodispose:autodispose-android:$autodispose"
  implementation "com.uber.autodispose:autodispose-android-archcomponents:$autodispose"
  implementation "com.uber.autodispose:autodispose-lifecycle:$autodispose"
}
